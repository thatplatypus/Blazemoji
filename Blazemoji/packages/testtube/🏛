📘
  Package providing basic unit testing utility.
📘

📗
  This class is a test helper class for unit testing.

  To create a test suite, subclass this class and override 🏁, for instance:

  ```
  🐇 🦔 🧪  🍇
    ✒️❗️ 🏁 🍇
      🔢👇 20 20 🔤20 equal 20🔤❗️
    🍉
  🍉
  ```

  Than instantiate your class and call 👔. The return of 👔 is an exit code
  that can be returned from the 🏁 function:

  ```
  🏁 ➡️ 🔢 🍇
    ↩️ 👔🆕🦔❗️❗️
  🍉
  ```
📗
🌍 🐇 🧪 🍇
  🖍🆕 asserts 🔢
  🖍🆕 failed 🔢

  🔓🆕
  📗
    Asserts that *value* is 👍. *description* should describe
    what this test asserts exactly.

    >!H Prefer other assertion methods like [[🔡❗️]] if possible as they
    >!H provide more information in the case of failure.
  📗
  🥯 🔐❗️ ⛔ value 👌 description 🔡 ➡️ ◼️ 🍇
    ↪️ value 🍇
      ✅👇 description❗️
    🍉
    🙅 🍇
      🙁👇 description❗️
    🍉
  🍉
  📗
    Asserts that *value* is 👎. *description* should describe
    what this test asserts exactly.
  📗
  🥯 🔐❗️ ❎ value 👌 description 🔡 ➡️ ◼️ 🍇
    ↪️ ❎value❗️ 🍇
      ✅👇 description❗️
    🍉
    🙅 🍇
      🙁👇 🔤🧲description🧲 but expected 👎 🔤❗️
    🍉
  🍉
  📗
    Asserts that *value* equals expectation. *description* should describe
    what this test asserts exactly.
  📗
  🥯 🔐❗️ 🔡 value 🔡 expectation 🔡 description 🔡 ➡️ ◼️ 🍇
    ↪️ value 🙌 expectation 🍇
      ✅👇 description❗️
    🍉
    🙅 🍇
      🙁👇 🔤🧲description🧲 but it is 🧲value🧲🔤❗️
    🍉
  🍉
  📗
    Asserts that *value* equals expectation. *description* should describe
    what this test asserts exactly.
  📗
  🥯 🔐❗️ 🔢 value 🔢 expectation 🔢 description 🔡 ➡️ ◼️ 🍇
    ↪️ value 🙌 expectation 🍇
      ✅👇 description❗️
    🍉
    🙅 🍇
      🙁👇 🔤🧲description🧲 but it is 🧲value🧲🔤❗️
    🍉
  🍉
  📗
    Asserts that *value* equals expectation. *description* should describe
    what this test asserts exactly.
  📗
  🥯 🔐❗️ 💧 value 💧 expectation 💧 description 🔡 ➡️ ◼️ 🍇
    ↪️ value 🙌 expectation 🍇
      ✅👇 description❗️
    🍉
    🙅 🍇
      🙁👇 🔤🧲description🧲 but it is 🧲value🧲🔤❗️
    🍉
  🍉
  📗
    Asserts that *value* equals expectation. *description* should describe
    what this test asserts exactly.
  📗
  🥯 🔐❗️ 💯 value 💯 expectation 💯 description 🔡 ➡️ ◼️ 🍇
    ↪️ value 🙌 expectation 🍇
      ✅👇 description❗️
    🍉
    🙅 🍇
      🙁👇 🔤🧲description🧲 but it is 🧲value🧲🔤❗️
    🍉
  🍉
  📗
    Adds an assertion failure with the provided description.
  📗
  🔐❗️ 🙁 description 🔡 ➡️ ◼️
  📗
    Adds a passed assertion with the provided description.
  📗
  🥯 🔐❗️ ✅ description 🔡 ➡️ ◼️ 🍇
    asserts ⬅️➕ 1
  🍉
  📗
    Runs tests.
  📗
  🔓❗️ 👔 ➡️ 🔢
  🥯 🔐❗️ 🏁 ➡️ ◼️ 🍇
    😀🔤You might want to implement the 🏁 method.🔤❗️
  🍉
🍉

