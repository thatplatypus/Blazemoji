📘
  The sockets package allows you to open TCP sockets to servers or to create a TCP server socket
  yourself.

  The following is a very basic example of opening a TCP socket to make an HTTP request and print
  the first 140 characters of the response.
  ```
  📦 sockets 🏠

  🏁 🍇
    🍺 🆕📞 🔤example.com🔤 80❗️ ➡️ socket
    💬 socket 📇 🔤GET / HTTP/1.1❌r❌nHost: example.com❌r❌n❌r❌n🔤❗️❗️

    🍺 👂 socket 140❗️ ➡️ data
    😀 🍺 🔡 data❗️❗️
  🍉
  ```

  At the time of writing this, the above code would return something similar to:
  ```
  HTTP/1.1 200 OK
  Cache-Control: max-age=604800
  Content-Type: text/html; charset=UTF-8
  Date: Mon, 10 Sep 2018 08:36:28 GMT
  Etag: "15410256
  ```

  Here we’ve an example of a minimal echo-server that listens on port 8728. The server simply sends
  back a copy of the data it received.
  ```
  📦 sockets 🏠

  💭 Simple echo server listening on port 8728
  🏁 🍇
    🍺🆕🏄 8728❗️ ➡️ server

    🔁 👍 🍇
      🍺 🙋 server❗️ ➡️ clientSocket

      🔁 👍 🍇
        🆗 data 👂 clientSocket 50❗️ 🍇
          💭 We’ve read 50 bytes and send them back
          💬 clientSocket data❗️
        🍉
        🙅‍♀️ error 🍇🍉
      🍉
    🍉
  🍉
  ```

  Of course, the code above is minimal. For example, it can handle only one
  connection.
📘

📗
  🏄 represents a socket that listens for clients on a given port.
📗
🌍 📻 🐇 🏄 🍇
  📗
    Creates a 🏄 instance that immediately starts listening on the given port.
    This initializer returns an error if an error occurs.
  📗
  🔓🆕port 🔢 🚧🚧🔸↕  📻 🔤socketsServerNewPort🔤
  📗
    Waits until a client wants to connect to this socket and returns a socket
    to communicate with it.
  📗
  🔓❗️ 🙋 ➡️ 📞🚧🚧🔸↕  📻 🔤socketsServerAccept🔤
  📗
    Closes this socket.
  📗
  🔓❗️ 🚪 ➡️ ◼️ 📻 🔤socketsServerClose🔤
  ♻️🍉

📗 📞 represents a socket for communication between client and server. 📗
🌍 📻 🐇 📞 🍇
  📗
    Opens a socket to *address*. *address* can be a host name which will be
    resolved.
  📗
  🔓🆕host 🔡 socket 🔢 🚧🚧🔸↕  📻 🔤socketsSocketNewHost🔤
  📗
    Sends the given data to the peer. Returns true if the data was successfully
    sent or false on error.
  📗
  🔓❗️ 💬 message 📇 ➡️ ◼️🚧🚧🔸↕  📻 🔤socketsSocketSend🔤
  📗
    Closes this socket.
  📗
  🔓❗️ 🚪 ➡️ ◼️ 📻 🔤socketsSocketClose🔤
  📗
    Tries to read up to *bytes* bytes from the socket. An error is returned
    on error or if the socket was closed by the peer.
  📗
  🔓❗️ 👂 bytes 🔢 ➡️ 📇🚧🚧🔸↕  📻 🔤socketsSocketRead🔤
  ♻️🍉

